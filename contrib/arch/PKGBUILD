pkgname=nodemanager
pkgver=0.1.30
pkgrel=1
pkgdesc='A Kubernetes controller to manage nodes'
arch=(x86_64 armv7l)
url="https://github.com/zachfi/$pkgname"
license=('APACHE')
makedepends=(git go)
_commit="v0.1.30"
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

# prepare(){
#   cd "$pkgname-$pkgver"
#   mkdir -p build/
# }

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd "${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  make build
}

check() {
  make -C $pkgname test
}

package() {
  cd $pkgname
  install -Dm755 bin/manager "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 contrib/arch/nodemanager.service "${pkgdir}/usr/lib/systemd/system/nodemanager.service"
}
